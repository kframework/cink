require "cink-basic-syntax.k"

module CINK-BASIC-SEMANTICS
  imports CINK-BASIC-SYNTAX


  /*@ \subsection{Values.} 
    The values are a very important syntactic category in the definition
    of any language; with them, we are able to know when the evaluation
    of an expression is finished. This piece of information is crucial
    for the heating and cooling rules. 

    In order to keep the definition
    as simple as possible, we let the expressions to be evaluated always
    to lvalues and then use the context \texttt{rvalue} to convert it into 
    an rvalue whenever this is neede. To be more precise, we consider the
    expression \texttt{++x + y}. According to \Cpp semantics, the evaluation
    of the operator \texttt{++} returns an lvalue. When  the operator
    \texttt{+} is evaluated this must be converted into an rvalue.
    Therefore the strict attribute of \texttt{+} has two sub-attributes:
    \texttt{context(rvalue)} and \texttt{result(RVal)}. The heating/cooling
    rules generated by strict attribute for the \texttt{+} operator are:
\begin{verbatim}
  E1:Exp + E2:Exp => rvalue(E1) ~> HOLE + E2 when notBool isRVal(E1)
  E1:Exp + E2:Exp => rvalue(E2) ~> E2 + HOLE when notBool isRVal(E2)
  I1:RVal ~> HOLE + E2 => I1 + E2
  I2:RVal ~> E1 + HOLE => E1 + I2
\end{verbatim}
    For instance, the above expression will be derived into the following
    sequence of computations (note that \texttt{rvalue} is strct, too; see
    below):
\begin{verbatim}
  ++x + y => rvalue(y) ~> ++x + HOLE => y ~> rvalue(HOLE) ~> ++x + HOLE 
\end{verbatim}
    After the variable \texttt{y} is evaluated to its lvalue (that is a
    location) $\ell_y$, the expression \texttt{rvalue($\ell_y$)} is 
    evaluated to \texttt{y} rvalue, i.e. the value stored in the location
    $\ell_y$. The first argument \texttt{++y} is evaluated in the same way.

    The same value, e.g. an location, could play both roles of lavalue and
    rvalue. Therefore we use a label \texttt{lval($v$)} saying that tha value
    $v$ plays the role of lvalue.

    We already defined the subset of values that is part of the syntax, 
    namely the values given by the primitive types. Here we estend the set
    o values with intermediate constructs needed to execute programs. 
    Such a value is\texttt{noVal}, which "paradoxally" denotes in fact 
    "no value". The lambda abstractions are used for storing the
    functions. Similar to other \K examples (IMP, IMPPP, SIMPLE), the 
    functions are stored similar to variables and therefore their definitions
    are seen as values.
    As usual, all values must be subsorted to the \texttt{KResult} sort.
   */


  syntax RVal ::= "noVal" 
               | "lambda" "(" Decls "@" Stmts ")" 
                 [latex(\lambda\,{#1}\mathrel{\bullet}{#2})]

  syntax Val ::= lval(Ref)

  //@ The empty expression is a value, as well:

  rule isVal(.Exps) => true

  //@ Any value is a K result:
  syntax KResult ::= Val

  //@ The definition of the strict context \texttt{rvalue}:
  
  syntax Exp ::= "rvalue" "(" Exp ")" [strict, klabel(rvalue)]

  /*@
    As its definition says, the main operator emphasiszing the two categories is
    the assignment. We use a context declaration to say that the left-hand side 
    of the assignemnt is a lvalue (by default):
  */

  context HOLE = _:Exp

  //@ When an lvalue is in an rvalue context, evaluate it
  rule <k> rvalue(lval(L:Ref) => V) ...</k>
       <store>... L |-> V:RVal ...</store>
       [kripke(mem-lookup)]

 /*@ \subsection{Seqpoints}

  /*@
    Sequence points are specific to languages where the evaluation of the
    expressions has side effects. Obviously, this is the case for C and \Cpp
    languages. Here is the definition for sequenced before from \Cpp manual:

\begin{quote}
  \emph{Sequenced before} is an asymmetric, transitive, pair-wise relation 
  between evaluations executed by a single
   thread (1.10), which induces a partial order among those evaluations.
   Given any two evaluations A and B, if
   A is sequenced before B, then the execution of A shall precede the execution
   of B. If A is not sequenced before
   B and B is not sequenced before A, then A and B are unsequenced.
  [ Note: The execution of unsequenced
   evaluations can overlap. — end note ] Evaluations A and B are 
  indeterminately sequenced when either A
   is sequenced before B or B is sequenced before A, but it is unspecified 
  which.
  [ Note: Indeterminately
   sequenced evaluations cannot overlap, but either could be executed first.
   — end note ]\\
  Every value computation and side effect associated with a full-expression
  is sequenced before every value
  computation and side effect associated with the next full-expression
  to be evaluated.\\
  Except where noted, evaluations of operands of individual operators and of 
  subexpressions of individual expressions are unsequenced.\\
  {}[Note: In an expression that is evaluated more than once during the execution
  of a program, unsequenced and indeterminately sequenced evaluations of its 
  subexpressions need not be performed consistently in different evaluations.
  —end note]\\
  The value computations of the operands of an operator are sequenced before
  the value computation of the result of the operator. If a side effect on a
  scalar object is unsequenced relative to either another side effect on the
  same scalar object or a value computation using the value of the same scalar
  object, the behavior is undefined.
\end{quote}
    In this iteration we define a minimal support for the sequent points.
    The semantics of a sequence point (when a full-expression is sequenced):
    the sequence of the side effects is moved from the its cell to the top of
    the k cell. The side-effects are executed in the reverse order they were
    evaluated.
  */

  syntax K ::= "seqpoint"

  rule <k> seqpoint => SE ...</k>
       <sideEffects> SE => . </sideEffects>

  //@ \textcolor{red}{TDB: explain "undefined" expression.}

  syntax BasicExp ::= "undefined"


  /*@ \subsection{Memory Locations}
   We use (automatically generated) symbolic values for locations.
   These are of sort \texttt{Loc}. The locations are by definition lvalues.
   */


  syntax Loc ::= "noLoc"
  syntax Ref ::= Loc 
  syntax RVal ::= Ref


  /*@ \subsection{Auxiliary constructs.} */


  /*@
    \texttt{execute} is used to start the computation of a program; and
    \texttt{noname} for the initial name of a thread.
  */
  syntax K ::= "execute"

 /*@
   The next two constructors are used for storing the enavironment and the
   rest of the computation in the call stack (\texttt{fstack}). 
 */

  syntax ListItem ::= "(" List "," K ")"
                    | "[" Map "]"

 /* \subsection{Configuration.} 
   Since \Cink is developed in iterations, each such iteration will have its
   own configuration. In order to have a modular definition, each module 
   adding semantics for several feature will include a minimal configuration
   needed to define these features.
   The following cells are used to give semantics for the basic constructs:
  */

/*
  configuration   <k color="green"> ($PGM:Pgm ~> execute) </k>
                  <env color="LightSkyBlue"> .Map </env>
                  <fstack color="orange"> .List </fstack>
                  <return> .K </return>
                  <br/>
                  <genv color="SkyBlue"> .Map </genv>
                  <store color="white"> .Map </store>
                  <in color="magenta" stream="stdin"> .List </in>
                  <out color="Orchid" stream="stdout"> .List </out>

*/
  
  /*@
    \subsection{Desugaring}
    In order to have a minimal set of rules, some syntactic constructs
    are desugared. The desugaring can be done using the structural rules.
   */

  /*@ The desugaring rule for the if-then statement: */
  rule if(B:Exp) St:Stmt => if(B) St else {}  [macro]
  
  /*@
    The desugaring rule for statements declaring multiple variables.
   */

  rule T:Type (E:Exp, E':Exp) ; => T E; T E'; [macro]

 /*@
    Desugaring rule for variable declarations with initialization.
    The alias declarations are defined later, together with the pointers.
  */

   rule T:Type E1:Exp = E2:Exp ; => T E1; E1 = E2;
        when isAliasExp(E1) =/=K true
        [macro]


  /*@ \subsection{Declarations.} */

  /*@
    Function declaration: a function is a stored similar to a variable,
    where the value stored in the associated location is the lambda
    abstraction of the function. 
   */


  rule <k>  (Decl:Decl (Xl:Decls) {Sts:Stmts})::Stmt  => .  ...</k> 
       <env>... . =>  (getName(Decl) |-> L) ...</env>
       <store>... . => (L |-> lambda(Xl @ Sts)) ...</store>
       when fresh(L:Loc)
       [fun-decl, structural]

  /*@
    The rules for variable declarations:
   */

  rule [var-decl] :
       <k> T:PrimType X:Id; => .  ...</k>      
       <env> Env:Map => Env[L/X] </env>
       <store>... . => (L |-> undefined) ...</store>
  when fresh(L:Loc)
       [structural]

  /*@
  Declaration of an alias:
  */

  rule isAliasExp(& E:Exp) => true

  context _:PrimType & _:Id = HOLE;

  rule <k> T:PrimType & X:Id = lval(L:Loc); => . ...</k>
       <env> Env:Map => Env[L/X] </env>

  /*@ The following two constructs have no semantics yet;
      they are used now only  for having a full compatibility with \Cpp,
      e.g., the \Cink programs can be 
      compiled with a \Cpp compiler. */
  rule #include <iostream> => . 
       [include, structural]

  rule using namespace std;=> .
       [using, structural]

  /*@ 
    The auxiliary construct \texttt{execute} is used to initialize the 
    execution of a program, which for the case of \Cink consists of the call
    of the main function.
  */

  rule <k> execute => String2Id("main")(.Exps); </k>
       <env> Env:Map </env>
       <genv> . => Env:Map </genv>
       [main, structural]
  
  /*@ \subsection{Expressions Evaluation.} */

  /*@
   The following expressions are strict. 
  */
  rule I1:Int + I2:Int => I1 +Int I2 [plus]

  rule I1:Int - I2:Int => I1 -Int I2 

  rule I1:Int * I2:Int => I1 *Int I2

  rule I1:Int / I2:Int => I1 /Int I2 when I2 =/=Int 0
 
  rule I1:Int % I2:Int => I1 %Int I2 when I2 =/=Int 0

  rule I1:Int < I2:Int => (I1 <Int I2)

  rule I1:Int > I2:Int => (I1 >Int I2)

  rule I1:Int <= I2:Int => (I1 <=Int I2)

  rule I1:Int == I2:Int => (I1 ==Int I2)
 
  rule true && B:Exp => B
  rule false && B:Exp => false
  rule true || B:Exp => true
  rule false || B:Exp => B 
  rule ! false  => true
  rule ! true => false 

  rule endl => "\n" [structural]

  /*@ \subsubsection{The prefix increment operator}
    Here is the description of this operator from the manual of 
    \Cpp 2011 (5.3.2):
\begin{quote}
    The operand of prefix ++ is modified by adding 1, or set to true 
    if it is bool (this use is deprecated). The operand shall be a 
    modifiable lvalue. The type of the operand shall be an arithmetic 
    type or a pointer to a completely-defined object type. 
    The result is the updated operand; it is an lvalue, and it is a 
    bit-field if the operand is a bit-field.
\end{quote}
  */

  //@ The semantics of the the prefix increment

  rule <k> ++ lval(L:Ref) => lval(L) ...</k>
       <store>... L |-> (V:Int => V +Int 1) ...</store>


  /*@
  \subsubsection{The postfix increment operator}
   Here is the description of this operator from the manual of 
  \Cpp 2011 (5.2.6):
\begin{quote}
  The value of a postfix ++ expression is the value of its operand.
  [ Note: the value obtained is a copy of
  the original value —end note ] The operand shall be a modifiable lvalue.
  The type of the operand shall be an arithmetic type or a pointer to a
  complete object type. The value of the operand object is modified by
  adding 1 to it, unless the object is of type bool, in which case it is set
  to true. [ Note: this use is deprecated, see Annex D. —end note ]
  The value computation of the ++ expression is sequenced before the
  modification of the operand object. With respect to an 
  indeterminately-sequenced function call, the operation of postfix
  ++ is a single evaluation.
  [ Note: Therefore, a function call shall not intervene between the
  lvalue-to-rvalue conversion and the side effect associated with any single
  postfix ++ operator. —end note ] The result is a prvalue.
\end{quote}
  From the Appendix D: 
\begin{quote}
  D.1 The use of an operand of type bool with the ++ operator is deprecated 
  (see 5.3.2 and 5.2.6)."
\end{quote}
  */

  /*@
   We add a new cell that collects the side-effects during the evaluation
   of a full-expression:
  */


  /*@
  we define a syntax for the side-effects;
  */

  syntax K ::= "$inc" "(" K ")" | "$dec" "(" K ")"

  
  /*@
    The semantics of the postfix increment operator: the result value is the 
    value  stored at the computed location and its side-effect is added to 
    the cell with the side-effects:
  */

  rule <k> lval(L:Ref) ++ => V ...</k>
       <store>... L |-> V:Int ...</store>
       <sideEffects> . => $inc(L) ...</sideEffects>


  //@ The semantics of the side-effects (self-explained);

  rule <k> $inc(L:Loc) => . ...</k>
       <store>... L |-> (V:Int => V +Int 1) ...</store>


  rule <k> $dec(L:Loc) => . ...</k>
       <store>... L |-> (V:Int => V -Int 1) ...</store>
 

  /*@ \subsection{Memmory operations.} */


  /*@
    The evaluation of a variable name as an lvalue:
  */
  rule <k> X:Id => lval(L) ...</k>
       <env>... X |-> L:Loc ...</env>

  /*@
    The memory update is given by the assignment operator.
    An assignemnt, after the reduction of the arguments, has in the lef-hand
    side a location ($=$ the lvalue designated by the lhs.) and in the 
    right-hand side an rvalue.
    From \Cpp manual:
\begin{quote}
The assignment operator (=) and the compound assignment operators all group
 right-to-left. All require a modifiable lvalue as their left operand and 
return an lvalue referring to the left operand. The result in all
cases is a bit-field if the left operand is a bit-field. In all cases,
 the assignment is sequenced after the value computation of the right and 
left operands, and before the value computation of the assignment expression.
With respect to an indeterminately-sequenced function call, the operation of
 a compound assignment is a single evaluation.\\
{}[ Note: Therefore, a function call shall not intervene between the 
lvalue-to-rvalue conversion and the side effect associated with any single 
compound assignment operator. —end note ]
\end{quote}
  */

  syntax K ::= "update" "(" Ref "," Val ")"

  rule lval(L:Ref) = V:Val => seqpoint ~> update(L, V)

  rule [update] : <k> update(L:Loc, V:Val)  => lval(L) ...</k>
       <store>... L |-> (_ => V) ...</store>
       [update, kripke]

/*@  The comma operator. 
    From \Cpp manual:

\begin{quote}
A pair of expressions separated by a comma is evaluated left-to-right; 
the left expression is a discardedvalue
expression (Clause 5). 
Every value computation and side effect associated with the left expression
is sequenced before every value computation and side effect associated with 
the right expression. The type
and value of the result are the type and value of the right operand; 
the result is of the same value category
as its right operand, and is a bit-field if its right operand is a glvalue and 
a bit-field. If the value of the right
operand is a temporary (12.2), the result is that temporary.
\end{quote}
*/

  rule V:Val,E:Exp => E

  /*@


  /* \subsection{Control Statements.} */

  /*@
    As usual, the \texttt{while} statement is desugared using the 
    \texttt{if-then-else} statement.
  */
  rule while(B:Exp) St:Stmt => if (B) {St while(B) St} else {}
       [while, structural]

  /*@
    Since \texttt{if} is strict in the first argument, which is a boolean
    expression, proceed by case-analysis on the result values:
  */
  rule if(false) _ else St:Stmt => St [if-false]
  rule if(true) St:Stmt else _ => St [if-true]


  /*@
    The semantics of the expression statement consists of removing the
    value obtained by evaluating the expression. Recall that the statement
    is strict.
   */
  rule V:Val; => .
       [instr-expr]

  /*@
    Block. Note that in this iteration we assume that the blocks do not include
    variable declarations. This will be added in a future iteration.\\
      -- the case of non-empty block
  */
  rule { Sts:Stmts } => Sts
       [block, structural]

  /*@ -- the case of the empty block: */
  rule {} => .  [block-empty, structural]

  //@ The sequential composition is just a sequence of computations:
  rule (Sts:Stmts Sts':Stmts) => (Sts ~> Sts')
       [seq-comp, structural] 

//  rule .Stmts => .K

  /*@ \subsection{Input/Output Statements.} */

  /*@ Writting in the the standard stream \texttt{cout}: */

  context HOLE << _

  rule <k> cout << V:RVal => cout  ...</k>  
       <out>... . => ListItem(V) </out>
       [print-stream, print]

  /*@
   In order to read from the standard stream \texttt{cin}, the expression
   from the right-hand side must be evaluated to an l-value:
  */

  rule <k> cin >> lval(L:Loc) => cin  ...</k>
       <store>... L |-> (_ => V)  ...</store>
       <in> (ListItem(V:Int) => .)  ...</in>
       [read-stream]


  /*@
    \subsection{The function call expression}
    The function name is evaluated to its value, which is a lambda 
    abstraction: $\it Xl$ is the list of parameters, $\it Sts$ is body of
    the function. The {\sc function-call} rule pushes the calling context,
    i.e., the remainder of the computation $K$ and environment stack 
    (including the current environment) on top of the function stack, 
    while the {\sc return} rule uses the information there to restore 
    the environment and computation of the caller.
    Since the evaluation strategy for the second argument is depending on
    the binding specification in the function signature, the function call
    expression is declared strict only in its first argument.
    Note that in this iteration we consider only the call-by-value 
    binding mechanism. The call-by-reference mechanism will be considered
    in a future iteration.
  /*

/*@
From the \Cpp manual:
\begin{quote}
When a function is called, each parameter (8.3.5) shall be initialized 
(8.5, 12.8, 12.1) with its corresponding
argument. [ Note: Such initializations are indeterminately sequenced with 
respect to each other (1.9) - end note ] \ldots\\  
When a function is called, the parameters that have object 
type shall have completely-defined
object type. \ldots \\
During the initialization of a parameter, an implementation may avoid the 
construction of extra temporaries by
combining the conversions on the associated argument and/or the construction
of temporaries with thevinitialization of the parameter (see 12.2). 
The lifetime of a parameter ends when the function in which it
is defined returns. The initialization and destruction of each parameter 
occurs within the context of the calling function.
\ldots\\{}
[ Note: a function can change the values of its non-const parameters, 
but these changes cannot affect the
values of the arguments except where a parameter is of a reference 
type (8.3.2)\ldots
\end{quote}
*/

  /*@
    The rule defining the evaluation of a function call expression evaluates
    first the actual parameters, and then binds the 
    values to the formal parameters and executes the body, while saving the
    calling context in case of an abrupt return. This is done by mimic the
    heating-cooling mechanism (the first rule is a heating-like one, and the
    second a cooling-like one).
   */

  rule <k> lambda(Xl:Decls @ Sts:Stmts)(El:Exp)
           =>
           evaluate append(El,.Exps) to .Exps following Xl; ~>
           lambda(Xl:Decls @ Sts:Stmts)(HOLE)
       ...</k> 

  rule <k> (evaluate .Exps to Vl following _; 
         ~> lambda(Xl:Decls @ Sts:Stmts)(HOLE) 
         ~> K:K)
           => 
           (bind Vl to Xl; ~> Sts ~> return noVal;)
       </k>
       <br/>
       <genv> GEnv:Map </genv>
       <env> Env:Map => GEnv </env>
       <fstack> . => ([Env], K) ...</fstack>
       [fun-call]

  /*@
    To evaluate actual parameters according to their declared strategy we will
    make use of the power of \K evaluation contexts. 
    The actual parameters must be evaluated using 
    the $\terminalNoSpace{evaluate}$ construct and $\terminalNoSpace{following}$ 
    the list of formal parameters.
  */

  syntax K ::= "evaluate" K "to" K "following" Decls ";"

  /*@
    For a formal parameter declared with the call-by-value mechanism,
    the corresponding argument expression must be evaluated to an rvalue as
    specified by by the following contextual declaration:
  */

  context evaluate ((HOLE => rvalue(HOLE)), _:Exp) to _ following (T X:Id, _:Decls);


  /*@
    \noindent This second context uses again the special type of context used
    above for $\terminalNoSpace{evaluate}$, by requesting that the expression
    on position $\khole{}$ be evaluated as an lvalue.
    \par
    The following two rules, together with the strict evaluation strategy for
    lists of expressions complete the semantics of $\terminalNoSpace{evaluate}$
    by recursing into the lists:
  */


  rule evaluate(V:Val, El => El) to (Vl => append(Vl, V))
         following (Dec:Decl, Xl:Decls => Xl);
       [structural]
   
   /*@
     \subsubsection{Binding mechanisms}
     Similarly to the evaluation rules, the binding rules are also different 
     for the two parameter passing styles.
     As we have already seen, the binding is performed using an auxiliary 
     construction:
   */

  syntax K ::= "bind" K "to" Decls ";"  

  /*@
  For call-by-value, the passed value $V$ is stored into a new memory location
  which is bound to the formal parameter:
  */
 
  rule <k> bind((V:Val, Vs) => Vs)
           to((T X:Id, Xl:Decls) => Xl); 
       ...</k>
       <env> Env:Map => Env[L/X] </env>
       <store>... . => L |-> V ...</store>
  when fresh(L:Loc)
       [structural]

  /*@
    Finally, once all parameters have been bound, the binding construct 
    dissolves:
   */
  rule bind .Exps to .Decls; => . [ structural ]

  /*@
    \subsection{Function Return.}
    Similar to \Cpp, the argument of a return statement is a list of
    expressions (note that Cink does not have yet a distinguished
    definition for the comma operator). 
  */

  /*@
    Similar to the comma operator, the evaluation of the return argument
    is given from left to right. Since the expression lists are strict,
    we get this feature for free provided the declaration for return is
    strict. Remains oly to write rules keeping the last value from the list.
  */

  rule <k> (return V:Val; ~> _) => (V ~> K) </k>
       <env> _ => Env </env>
       <fstack> ([Env:Map], K:K) => . ...</fstack>
       <return> _ => V </return>
       [return]

  /*@
    \subsection{Auxiliary Functions and Rules.}
    Here we winclude the semantics for the auxiliary constructs, used to give
    semnatics for \Cink.\\
    The "alias" are used for declaring aliases for the variables; their semantics 
    is given in the iteration that adds the pointers. 
  */

  syntax Bool ::= "isAliasExp" "(" Exp ")" [function]

  /*@
    The next operator returns the variable name from an expression occurring
    in a declaration:
  */
  syntax  Id ::= "getName" "(" K ")" [function]

  rule getName(X:Id) => X
  rule getName(T:Type E:Exp) => getName(E)
  rule getName(E1:Exp = E2:Exp) => getName(E1)
  rule getName(E1:Exp,E2:Exp) => getName(E2)

  //@ Append function:

  syntax Exp ::= "append" "(" Exp "," Exp ")" [function]
  rule append((E:Exp, El:Exp), E') => E, append(El, E')
  rule append(.Exps, .Exps) => .Exps
  rule append(.Exps, E:Exp) => E,.Exps
    when E =/=K .Exps
  rule append(E:Exp,E') => E, E' [owise]

endmodule
